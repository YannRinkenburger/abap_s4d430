@Metadata.layer: #CUSTOMER

@UI.headerInfo: { typeName: 'Movie',
                  typeNamePlural: 'Movies',
                  title.value: 'Title',
                  description.value: 'PublishingYear',
                  imageUrl: 'ImageUrl' }

@UI.presentationVariant: [ { sortOrder: [ { by: 'AverageRating', direction: #DESC } ] } ]

annotate entity ZC_01_Movie
  with

{
  @UI.facet: [ { position: 10, type: #FIELDGROUP_REFERENCE, label: 'Movie Details', targetQualifier: 'Details' },
               { position: 20, type: #FIELDGROUP_REFERENCE, label: 'Administrative Data', targetQualifier: 'Admin' },
               { position: 30, type: #LINEITEM_REFERENCE, label: 'Ratings', targetElement: '_Ratings' } ]
               
  @UI.lineItem: [{ position: 5, type: #FOR_ACTION, label:'Rate Movie', dataAction:'RateMovie' }]
  
  @UI.fieldGroup: [ { position: 10, qualifier: 'Admin' } ]
  MovieUuid;

  @UI.fieldGroup: [ { position: 10, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 10, importance: #HIGH } ]
  Title;

  @UI.fieldGroup: [ { position: 20, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 20, importance: #HIGH } ]
  @UI.selectionField: [ { position: 10 } ]
  Genre;

  @UI.fieldGroup: [ { position: 30, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 30, importance: #HIGH } ]
  @UI.selectionField: [ { position: 20 } ]
  PublishingYear;

  @UI.fieldGroup: [ { position: 40, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 40, importance: #HIGH } ]
  @UI.selectionField: [ { position: 30 } ]
  RuntimeInMin;

  @UI.dataPoint: { visualization: #RATING, targetValue: 10 }
  @UI.fieldGroup: [ { position: 45, qualifier: 'Details', criticality: 'AverageRatingCriticality' } ]
  @UI.lineItem: [ { label: 'Rating',
                    valueQualifier: 'Rating',
                    type: #AS_DATAPOINT,
                    position: 100,
                    importance: #HIGH } ]
  @UI.selectionField: [ { position: 40 } ]
  AverageRating;

  @UI.fieldGroup: [ { position: 50, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 50, importance: #HIGH } ]
  ImageUrl;

  @UI.fieldGroup: [ { position: 20, qualifier: 'Admin' } ]
  CreatedAt;

  @UI.fieldGroup: [ { position: 30, qualifier: 'Admin' } ]
  CreatedBy;

  @UI.fieldGroup: [ { position: 40, qualifier: 'Admin' } ]
  LastChangedAt;

  @UI.fieldGroup: [ { position: 50, qualifier: 'Admin' } ]
  LastChangedBy;
  
  @UI.hidden: true
  AverageRatingCriticality;
}